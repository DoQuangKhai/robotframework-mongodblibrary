*** Settings ***
Library   de.codecentric.robot.mongodblibrary.keywords.MongodbLibrary
Library	  Collections
Suite Setup  Startup Embedded  2.4.4
Suite TearDown  Shutdown Embedded
Test Setup  Setup MongoDB

*** Test Cases ***
should insert given document
	Insert Document  myCollection  {say : 'Hello MongoDb!'}
	Collection Should Exist  myCollection
	Document Should Exist  myCollection  {say : 'Hello MongoDb!'}
should insert data from file
	Import Documents  myCollection  sample/data.json
	Collection Should Exist  myCollection
	Document Should Exist  myCollection  {name : 'Mike'}
should insert data from file (row-seperated)
	Import Documents Row Seperated  myCollection  sample/dataMultipleRows.json
	Collection Should Exist  myCollection
	Document Should Exist  myCollection  {name : 'Mike', age : 22}
	Document Should Exist  myCollection  {name : 'Tom', age : 25}
	Document Should Exist  myCollection  {name : 'Eric', age : 40}
	${allDocuments}=  Get All Documents  myCollection
	${document1}=  Get From List  ${allDocuments}  0
	${document1Age}=  Get From Dictionary  ${document1}  age
	${expectedAge}=  Convert To Integer  22
	Should Be Equal  ${expectedAge}  ${document1Age}  age isn't 22  
*** Keywords ***
Setup MongoDB
	Connect To Server  localhost  27020  robotdb1
	Drop Database  robotdb1