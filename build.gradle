project.version = '0.1'

repositories { mavenCentral() }

apply plugin: 'java'
apply plugin: 'eclipse'

configurations { 
	robot
	docs
}

dependencies {
	compile group: 'junit', name: 'junit', version: "4.10"
	compile group: 'org.apache.commons', name: 'commons-io', version: "1.3.2"
	compile group: 'com.github.ombre42', name: 'jrobotremoteserver', version: "2.0"
	compile group: 'org.mongodb', name: 'mongo-java-driver', version: "2.10.0"
	robot group: 'org.robotframework', name: 'robotframework', version: "2.7.5"
	robot group: 'org.robotframework', name: 'javalib-core', version: "1.0.3"
	docs files("${System.getProperty('java.home')}/../lib/tools.jar")
}

/*
 *  tasks
 */
task(type : Jar, 'jar with dependencies') {
	classifier = 'with-dependencies'
	exclude('META-INF/*.RSA') 
	exclude('META-INF/*.SF') 
	from { configurations.compile.collect { 
			if (!it.isDirectory()) {
				zipTree(it) 
			}
		}
	}
	from { sourceSets.main.output }
}

assemble.dependsOn 'jar with dependencies'

task(type : JavaExec, 'run sample tests') {
	classpath = configurations.robot + configurations.compile + sourceSets.main.runtimeClasspath
	main = 'org.robotframework.RobotFramework'
	args = [
		'-d',
		'build',
		'sample/mongodblibrarySample.txt'
	]
}

task 'generate docs' << {
	tasks['generate html doc'].exec()
	tasks['generate xml doc'].exec()
}

task(type : JavaExec, 'generate html doc') {
	classpath = configurations.docs + configurations.robot + configurations.compile + sourceSets.main.runtimeClasspath
	main = 'org.python.util.jython'
	args = [
		'-m',
		'robot.libdoc',
		'src/main/java/de/codecentric/robot/mongodblibrary/keywords/MongodbLibrary.java',
		'doc/libdoc/de.codecentric.robot.mongodblibrary.keywords.MongodbLibrary.html'
	]
}

task(type : JavaExec, 'generate xml doc') {
	classpath = configurations.docs + configurations.robot + configurations.compile + sourceSets.main.runtimeClasspath
	main = 'org.python.util.jython'
	args = [
		'-m',
		'robot.libdoc',
		'--format',
		'XML',
		'src/main/java/de/codecentric/robot/mongodblibrary/keywords/MongodbLibrary.java',
		'doc/libdoc/de.codecentric.robot.mongodblibrary.keywords.MongodbLibrary.xml'
	]
}